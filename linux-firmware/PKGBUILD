# Maintainer: Maximilian Luz <luzmaximilian@gmail.com>
#
# Based on original Arch Linux linux-firmware PKGBUILD
#
# Note:
#   This package requires external firmware files. You can use the `getfw.py`
#   script at https://github.com/linux-surface/aarch64-firmware to obtain those
#   from a Windows recovery image or a Windows partition. The path to the
#   created firmware directory needs to be passed in the SPX_FIRMWARE_ROOT
#   environment variable.
#
# Usage:
#   env SPX_FIRMWARE_ROOT="../path/to/firmware/" makepkg

_pkgbase=linux-firmware
pkgbase=${_pkgbase}-oneplus-sdm845
pkgname=(${pkgbase}-whence ${pkgbase} ${pkgbase}-qcom)
_tag=20220509
pkgver=20220509.b19cbdc
pkgrel=1
pkgdesc="Firmware files for Linux"
url="https://git.kernel.org/?p=linux/kernel/git/firmware/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
arch=('any')
makedepends=('git')
options=(!strip)

source=(
  "git+https://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git#tag=${_tag}"
  "git+https://gitlab.com/sdm845-mainline/firmware-oneplus-sdm845.git"
)
sha256sums=('SKIP'
            'SKIP')

_backports=(
)

prepare() {
  cd ${_pkgbase}

  msg2 "Applying distro patches"

  local _c
  for _c in "${_backports[@]}"; do
    git log --oneline -1 "${_c}"
    git cherry-pick -n "${_c}"
  done

  msg2 "Adding/patching firmware for sdm845 devices"
  cp -a "${srcdir}/firmware-oneplus-sdm845/lib/firmware/postmarketos/." .
  cp -a "${srcdir}/firmware-oneplus-sdm845/lib/firmware/." .
  cp -a "../../tfa98xx.cnt" .
  # update WHENCE
  echo "" >> WHENCE
  echo "---------------------------------------------------------------------------" >> WHENCE
  echo "" >> WHENCE
  echo "Driver: sdm845 devices firmware" >> WHENCE
  echo "" >> WHENCE
  for file in $(git ls-files --others --exclude-standard); do
    # ignore already compressed files
    if [[ $file == *.xz ]]; then
      continue
    fi

    # handle links properly
    if test -L "${file}"; then
      dir=$(dirname "${file}")
      target=$(readlink -f "${file}")
      target=$(realpath --relative-to="${dir}" "${target}")
      echo "Link: ${file} -> ${target}" >> WHENCE
    else
      echo "File: ${file}" >> WHENCE
    fi
  done
  echo "Version: 1.0.0" >> WHENCE
  echo "" >> WHENCE
  echo "Licence: Proprietary" >> WHENCE
}

pkgver() {
  cd ${_pkgbase}

  # Commit date + short rev
  echo $(TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD).$(git rev-parse --short HEAD)
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_linux-firmware-oneplus-sdm845-whence() {
  pkgdesc+=" - contains the WHENCE license file which documents the vendor license details"
  provides=('linux-firmware-whence')
  conflicts=('linux-firmware-whence')

  install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 ${_pkgbase}/WHENCE
}

package_linux-firmware-oneplus-sdm845() {
  depends=('linux-firmware-oneplus-sdm845-whence')
  provides=('linux-firmware')
  conflicts=('linux-firmware')

  cd ${_pkgbase}

  make DESTDIR="${pkgdir}" FIRMWAREDIR=/usr/lib/firmware install

  # Trigger a microcode reload for configurations not using early updates
  echo 'w /sys/devices/system/cpu/microcode/reload - - - - 1' |
    install -Dm644 /dev/stdin "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 LICEN*

  # split
  cd "${pkgdir}"

  _pick linux-firmware-nfp usr/lib/firmware/netronome
  _pick linux-firmware-nfp usr/share/licenses/${pkgname}/LICENCE.Netronome

  _pick linux-firmware-mellanox usr/lib/firmware/mellanox

  _pick linux-firmware-marvell usr/lib/firmware/{libertas,mwl8k,mwlwifi,mrvl}
  _pick linux-firmware-marvell usr/share/licenses/${pkgname}/LICENCE.{Marvell,NXP}

  _pick linux-firmware-qcom usr/lib/firmware/{qcom,a300_*}
  _pick linux-firmware-qcom usr/share/licenses/${pkgname}/LICENSE.qcom

  _pick linux-firmware-liquidio usr/lib/firmware/liquidio
  _pick linux-firmware-liquidio usr/share/licenses/${pkgname}/LICENCE.cavium_liquidio

  _pick linux-firmware-qlogic usr/lib/firmware/{qlogic,qed,ql2???_*,c{b,t,t2}fw-*}
  _pick linux-firmware-qlogic usr/share/licenses/${pkgname}/LICENCE.{qla1280,qla2xxx}

  _pick linux-firmware-bnx2x usr/lib/firmware/bnx2x*
}

package_linux-firmware-oneplus-sdm845-qcom() {
  pkgdesc+=" - qcom / Firmware for Qualcomm SoCs"
  depends=('linux-firmware-oneplus-sdm845-whence')
  provides=('linux-firmware-qcom')
  conflicts=('linux-firmware-qcom')

  mv -v linux-firmware-qcom/* "${pkgdir}"
}

# vim:set sw=2 et:
