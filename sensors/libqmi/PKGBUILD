# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libqmi
pkgver=1.33.4
pkgrel=1
pkgdesc="QMI modem protocol helper library"
url="https://www.freedesktop.org/wiki/Software/libqmi/"
arch=(aarch64)
license=(GPL2)
depends=(libmbim libqrtr-glib libgudev)
makedepends=(gtk-doc meson git help2man gobject-introspection bash-completion)
provides=(libqmi-glib.so)
_commit=0d1980eef16cc0f1357d555b70606f852db2dc97  # tags/1.32.4^0
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libqmi.git#commit=$_commit"
        "0001-data-qmi-service-ssc-add-SSC-service.patch"
        "0002-libqmi-glib-message-handle-QMUX-and-QRTR-messages.patch"
        "0003-libqmi-glib-proxy-handle-QMUX-and-QRTR-messages.patch"
        "0004-libqmi-glib-endpoint-handle-QMUX-and-QRTR-messages.patch"
        "0005-libqmi-glib-device-handle-QMUX-and-QRTR-messages.patch"
        "0006-libqmi-glib-endpoint-qrtr-handle-QMUX-and-QRTR-messa.patch"
        "0007-libqmi-glib-port-context-handle-QMUX-and-QRTR-messag.patch"
)
sha256sums=('SKIP'
            '7e68de2d5e6c41e0814691a7372c74adc271d45950aa30c70a8687c8ec2e9f56'
            '4b804b5278416e95a1b75629726eb4df569defea014cfe8ce57e2996e96e7102'
            '4836f5bd497b3db210fa2aa2ea0b7f135dda75cffd5900f2eda8d587628f2412'
            '2ea1087e50ecd87e8b4899f3958abf0047129e6d20452cf749e7e60d5c9c6355'
            'd11c9e6df15a330718ebe247595119c4faf25358365235e9af60f89377588683'
            '91642a57894b602a187ba668e99faed84016cfd1cdb7aeb476e1a6bd02949562'
            '54a52de328a1e8d20a20f8e2c1e2ef2ee9a8b9fed3d8c1bcf9df32244f7224c4')

pkgver() {
  cd libqmi
  git describe --tags | sed 's/-rc/rc/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libqmi
    patch -Np1 -i ../0001-data-qmi-service-ssc-add-SSC-service.patch
    patch -Np1 -i ../0002-libqmi-glib-message-handle-QMUX-and-QRTR-messages.patch
    patch -Np1 -i ../0003-libqmi-glib-proxy-handle-QMUX-and-QRTR-messages.patch
    patch -Np1 -i ../0004-libqmi-glib-endpoint-handle-QMUX-and-QRTR-messages.patch
    patch -Np1 -i ../0005-libqmi-glib-device-handle-QMUX-and-QRTR-messages.patch
    patch -Np1 -i ../0006-libqmi-glib-endpoint-qrtr-handle-QMUX-and-QRTR-messa.patch
    patch -Np1 -i ../0007-libqmi-glib-port-context-handle-QMUX-and-QRTR-messag.patch

}

build() {
  arch-meson libqmi build
  meson compile -C build
}



package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
